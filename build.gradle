apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven'

group = 'com.laamella.markdown_to_asciidoc'
sourceCompatibility = targetCompatibility = 1.6
defaultTasks 'check'

repositories {
  if (project.hasProperty('useMavenLocal') && project.useMavenLocal.toBoolean()) {
    mavenLocal()
  }
  mavenCentral()
}

dependencies {
  compile 'org.pegdown:pegdown:1.4.2'
  compile 'org.jsoup:jsoup:1.8.1'

  testCompile 'info.cukes:cucumber-picocontainer:1.1.8'
  testCompile 'info.cukes:cucumber-junit:1.1.8'
  testCompile 'junit:junit:4.12'
  testCompile 'org.apache.commons:commons-io:1.3.2'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  from sourceSets.main.allSource
  classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
  classifier 'javadoc'
}

startScripts {
  applicationName = rootProject.name
  mainClassName = 'com.laamella.markdown_to_asciidoc.Converter'
}

artifacts {
  archives sourcesJar, javadocJar, distZip
}
